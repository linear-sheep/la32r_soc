module axi_dvi #
(
    parameter WIDTH = 12,    // hdata and vdata width (in bits)
    parameter HSIZE = 800,   // Horizontal size of visible area
    parameter HFP = 856,     // Horizontal front porch
    parameter HSP = 976,     // Horizontal sync pulse
    parameter HMAX = 1040,   // Horizontal total size
    parameter VSIZE = 600,   // Vertical size of visible area
    parameter VFP = 637,     // Vertical front porch
    parameter VSP = 643,     // Vertical sync pulse
    parameter VMAX = 666,    // Vertical total size
    parameter HSPP = 1,      // Horizontal sync pulse polarity (1 for positive)
    parameter VSPP = 1       // Vertical sync pulse polarity (1 for positive)
)
(
    input            s_awvalid,
    output           s_awready,
    input   [31:0]   s_awaddr,
    input   [4:0]    s_awid,
    input   [7:0]    s_awlen,
    input   [2:0]    s_awsize,
    input   [1:0]    s_awburst,
    input   [0:0]    s_awlock,
    input   [3:0]    s_awcache,
    input   [2:0]    s_awprot,
    input            s_wvalid,
    output           s_wready,
    input   [31:0]   s_wdata,
    input   [3:0]    s_wstrb,
    input            s_wlast,
    output           s_bvalid,
    input            s_bready,
    output  [4:0]    s_bid,
    output  [1:0]    s_bresp,
    input            s_arvalid,
    output           s_arready,
    input   [31:0]   s_araddr,
    input   [4:0]    s_arid,
    input   [7:0]    s_arlen,
    input   [2:0]    s_arsize,
    input   [1:0]    s_arburst,
    input   [0:0]    s_arlock,
    input   [3:0]    s_arcache,
    input   [2:0]    s_arprot,
    output           s_rvalid,
    input            s_rready,
    output  [31:0]   s_rdata,
    output  [4:0]    s_rid,
    output  [1:0]    s_rresp,
    output           s_rlast,
    
    // VRAM Interface
    output  [31:0]   fb_base,
    output           fb_en,
    output           fb_valid,
    input   [31:0]   fb_pixel,  // RGB332 扩展后的 32bit 数据

    // DVI/VGA Output 
    output  video_clk,
    output  hsync,
    output  vsync,
    output  data_enable,
    output  [2:0] video_red,
    output  [2:0] video_green,
    output  [1:0] video_blue,

    input            aclk,
    input            aresetn
);

    reg [31:0] DVI_RECT_DIR, DVI_RECT_L_W, DVI_SQU_DIR, DVI_SQU_R;
    reg [31:0] VRAM_BASE_ADDR;  // 0x10: 显存基地址
    reg [31:0] VRAM_CTRL;       // 0x14: 显存控制位 (Bit 0为1使能)

    reg busy, write, R_or_W;
    reg s_wready;

    wire ar_enter = s_arvalid & s_arready;
    wire r_retire = s_rvalid & s_rready & s_rlast;
    wire aw_enter = s_awvalid & s_awready;
    wire w_enter  = s_wvalid & s_wready & s_wlast;
    wire b_retire = s_bvalid & s_bready;

    assign s_arready = ~busy & (!R_or_W | !s_awvalid);
    assign s_awready = ~busy & ( R_or_W | !s_arvalid);

    always@(posedge aclk)
        if(~aresetn) busy <= 1'b0;
        else if(ar_enter | aw_enter) busy <= 1'b1;
        else if(r_retire | b_retire) busy <= 1'b0;

    reg [4 :0] buf_id;
    reg [31:0] buf_addr;
    reg [7 :0] buf_len;
    reg [2 :0] buf_size;
    reg [1 :0] buf_burst;
    reg        buf_lock;
    reg [3 :0] buf_cache;
    reg [2 :0] buf_prot;

    always@(posedge aclk) begin
        if(~aresetn) begin
            R_or_W      <= 1'b0;
            buf_id      <= 'b0;
            buf_addr    <= 'b0;
            buf_len     <= 'b0;
            buf_size    <= 'b0;
            buf_burst   <= 'b0;
            buf_lock    <= 'b0;
            buf_cache   <= 'b0;
            buf_prot    <= 'b0;
        end
        else if(ar_enter | aw_enter) begin
            R_or_W      <= ar_enter;
            buf_id      <= ar_enter ? s_arid   : s_awid   ;
            buf_addr    <= ar_enter ? s_araddr : s_awaddr ;
            buf_len     <= ar_enter ? s_arlen  : s_awlen  ;
            buf_size    <= ar_enter ? s_arsize : s_awsize ;
            buf_burst   <= ar_enter ? s_arburst: s_awburst;
            buf_lock    <= ar_enter ? s_arlock : s_awlock ;
            buf_cache   <= ar_enter ? s_arcache: s_awcache;
            buf_prot    <= ar_enter ? s_arprot : s_awprot ;
        end
    end

    always@(posedge aclk)
        if(~aresetn) write <= 1'b0;
        else if(aw_enter) write <= 1'b1;
        else if(ar_enter)  write <= 1'b0;

    always@(posedge aclk)
        if(~aresetn) s_wready <= 1'b0;
        else if(aw_enter) s_wready <= 1'b1;
        else if(w_enter & s_wlast) s_wready <= 1'b0;

    reg [31:0] s_rdata;
    reg s_rvalid, s_rlast;
    
    wire [31:0] rdata_d =   buf_addr[15:0] ==  16'h0     ? DVI_RECT_DIR         : 
                            buf_addr[15:0] ==  16'h4     ? DVI_RECT_L_W         : 
                            buf_addr[15:0] ==  16'h8     ? DVI_SQU_DIR          : 
                            buf_addr[15:0] ==  16'hc     ? DVI_SQU_R            : 
                            buf_addr[15:0] ==  16'h10    ? VRAM_BASE_ADDR       :
                            buf_addr[15:0] ==  16'h14    ? VRAM_CTRL            :
                            32'd0;

    always@(posedge aclk) begin
        if(~aresetn) begin
            s_rdata  <= 'b0;
            s_rvalid <= 1'b0;
            s_rlast  <= 1'b0;
        end
        else if(busy & !write & !r_retire) begin
            s_rdata  <= rdata_d;
            s_rvalid <= 1'b1;
            s_rlast  <= 1'b1; 
        end
        else if(r_retire) begin
            s_rvalid <= 1'b0;
        end
    end

    reg s_bvalid;
    always@(posedge aclk) begin
        if(~aresetn) s_bvalid <= 1'b0;
        else if(w_enter) s_bvalid <= 1'b1;
        else if(b_retire) s_bvalid <= 1'b0;
    end
    
    assign s_rid   = buf_id;
    assign s_bid   = buf_id;
    assign s_bresp = 2'b0;
    assign s_rresp = 2'b0;

//------------------------------- AXI Write Regs ----------------------------//
    wire write_reg_en[5:0];
    assign write_reg_en[0] = w_enter & (buf_addr[15:0]==16'h0);
    assign write_reg_en[1] = w_enter & (buf_addr[15:0]==16'h4);
    assign write_reg_en[2] = w_enter & (buf_addr[15:0]==16'h8);
    assign write_reg_en[3] = w_enter & (buf_addr[15:0]==16'hc);
    assign write_reg_en[4] = w_enter & (buf_addr[15:0]==16'h10);
    assign write_reg_en[5] = w_enter & (buf_addr[15:0]==16'h14);

    always @(posedge aclk) begin
        if(!aresetn) begin
            DVI_RECT_DIR <= 32'h0;
            DVI_RECT_L_W <= 32'h0;
            DVI_SQU_DIR  <= 32'h0;
            DVI_SQU_R    <= 32'h0;
            VRAM_BASE_ADDR <= 32'h0;
            VRAM_CTRL      <= 32'h0;
        end
        else begin
            if (write_reg_en[0]) DVI_RECT_DIR <= s_wdata;
            if (write_reg_en[1]) DVI_RECT_L_W <= s_wdata;
            if (write_reg_en[2]) DVI_SQU_DIR  <= s_wdata;
            if (write_reg_en[3]) DVI_SQU_R    <= s_wdata;
            if (write_reg_en[4]) VRAM_BASE_ADDR <= s_wdata;
            if (write_reg_en[5]) VRAM_CTRL      <= s_wdata;
        end
    end

//------------------------------- Video Timing & Output ----------------------------//
    reg [WIDTH-1:0] hdata = 0;
    reg [WIDTH-1:0] vdata = 0;   
    
    reg [31:0] current_vram_base;
    reg        current_vram_en;
    
    always @(posedge aclk) begin
        if (!aresetn) begin
            hdata <= 0;
            vdata <= 0;
            current_vram_base <= 32'd0;
            current_vram_en   <= 1'b0;
        end else begin
            if (hdata == (HMAX - 1)) begin
                hdata <= 0;
                if (vdata == (VMAX - 1)) begin
                    vdata <= 0;
                    // 在新一帧开始时更新 VRAM 配置以防画面撕裂
                    current_vram_base <= VRAM_BASE_ADDR;
                    current_vram_en   <= VRAM_CTRL[0];
                end else begin
                    vdata <= vdata + 1;
                end
            end else begin
                hdata <= hdata + 1;
            end
        end
    end

    wire hdata_in_range  = (hdata > (DVI_RECT_DIR[31:16]-DVI_RECT_L_W[31:16])) && (hdata < (DVI_RECT_DIR[31:16]+DVI_RECT_L_W[31:16]));
    wire vdata_in_range  = (vdata > DVI_RECT_DIR[15:0]) && (vdata < (DVI_RECT_DIR[15:0]+DVI_RECT_L_W[15:0]));
    wire hdata1_in_range = (hdata > (DVI_SQU_DIR[31:16]-DVI_SQU_R[31:16])) && (hdata < (DVI_SQU_DIR[31:16]+DVI_SQU_R[31:16]));
    wire vdata1_in_range = (vdata > (DVI_SQU_DIR[15:0]-DVI_SQU_R[15:0])) && (vdata < (DVI_SQU_DIR[15:0]+DVI_SQU_R[15:0]));
    
    wire in_visible_area = (hdata < HSIZE) && (vdata < VSIZE);
    
    assign fb_base  = current_vram_base;
    assign fb_en    = current_vram_en;
    assign fb_valid = current_vram_en && in_visible_area;

    // 补偿 VRAM 读取的 1 拍延迟，将同步信号与坐标系打拍对齐
    wire hsync_raw = ((hdata >= HFP) && (hdata < HSP)) ? HSPP : !HSPP;
    wire vsync_raw = ((vdata >= VFP) && (vdata < VSP)) ? VSPP : !VSPP;
    wire shape_active_raw = (hdata_in_range && vdata_in_range) || (hdata1_in_range && vdata1_in_range);

    reg hsync_d, vsync_d, data_enable_d, shape_active_d;

    always @(posedge aclk) begin
        if (!aresetn) begin
            hsync_d <= !HSPP;
            vsync_d <= !VSPP;
            data_enable_d <= 1'b0;
            shape_active_d <= 1'b0;
        end else begin
            hsync_d <= hsync_raw;
            vsync_d <= vsync_raw;
            data_enable_d <= in_visible_area;
            shape_active_d <= shape_active_raw;
        end
    end

    assign video_clk   = aclk;
    assign hsync       = hsync_d;
    assign vsync       = vsync_d;
    assign data_enable = data_enable_d;

    // 色彩混合：优先输出 VRAM 图层，否则输出原始形状区域
    assign video_red   = data_enable_d ? (current_vram_en ? fb_pixel[23:21] : (shape_active_d ? 3'b111 : 3'b000)) : 3'b000;
    assign video_green = data_enable_d ? (current_vram_en ? fb_pixel[15:13] : (shape_active_d ? 3'b000 : 3'b000)) : 3'b000;
    assign video_blue  = data_enable_d ? (current_vram_en ? fb_pixel[7:6]   : (shape_active_d ? 2'b00  : 2'b00))  : 2'b00;

endmodule
